* https://adventofcode.com/2024/day/4
OSREAD WORDS.RAW FROM 'AOC4.DAT' ELSE ABORT
CONVERT CHAR(10) TO @AM IN WORDS.RAW
WORDS=''
FOR R=1 TO DCOUNT(WORDS.RAW,@AM)
  FOR C=1 TO LEN(WORDS.RAW<R>)
    WORDS<R,-1>=WORDS.RAW<R>[C,1]
  NEXT C
NEXT R
*
MAX.R=DCOUNT(WORDS, @AM)
MAX.C=DCOUNT(WORDS<1>,@VM) ;* Assume all rows are the same length
XMAS.COUNT=0
CHECK.WORD='MAS'
FOR R=1 TO DCOUNT(WORDS,@AM)
  FOR C=1 TO DCOUNT(WORDS<R>,@VM)
    IF WORDS<R,C> # 'X' THEN CONTINUE ;* Starting point will always be an X
    * Check all 8 directions
    FOR DR=-1 TO 1
      FOR DC=-1 TO 1
        IF DR=0 AND DC=0 THEN CONTINUE ;* (-1,0,1) squared is 9 options, we just want the 8 directions
        FOUND=1 ; R.NEXT=R ; C.NEXT=C
        FOR T=1 TO 3
          R.NEXT+=DR
          C.NEXT+=DC
          IF R.NEXT > MAX.R OR R.NEXT < 1 OR C.NEXT > MAX.C OR C.NEXT < 1 THEN FOUND=0 ; EXIT ;* Out of bounds, bail
          IF WORDS<R.NEXT,C.NEXT> # CHECK.WORD[T,1] THEN FOUND=0 ; EXIT ;* Not a match, bail
        NEXT T
        IF FOUND THEN XMAS.COUNT+=1
      NEXT DC
    NEXT DR
  NEXT C
NEXT R
CRT 'XMAS Count: ':XMAS.COUNT
STOP
*
