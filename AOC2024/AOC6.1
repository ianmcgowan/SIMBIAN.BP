* https://adventofcode.com/2024/day/6
OSREAD MAP FROM 'AOC6.DAT' ELSE ABORT
CONVERT CHAR(10) TO @AM IN MAP
DEL MAP<DCOUNT(MAP,@AM)> ;* Get rid of trailing blank line
*
PRINT @(-1):
MAX.ROWS=DCOUNT(MAP,@AM)
MAX.COLS=LEN(MAP<1>)
FOR R=1 TO MAX.ROWS
  CRT MAP<R>
  C=INDEX(MAP<R>,'^',1)
  IF C THEN GUARD.ROW=R ; GUARD.COL=C
NEXT R
MAP.VISITED=''
*
DIRS='N,E,S,W'
DIRS<2>='-1,0,1,0' ;* Change in rows for dir
DIRS<3>='0,1,0,-1' ;* Change in cols for dir
DIRS<4>='^,>,v,<'  ;* Character to use for each direction
CONVERT ',' TO @VM IN DIRS
*
* Starting out guard is facing North ^
DIR='N'
GOSUB GET.DIR
*
DONE=0
LOOP
  MAP.VISITED<GUARD.ROW,GUARD.COL>=1
  GOSUB GET.NEXT.POS
WHILE NOT(DONE) DO
  IF BLOCKED THEN
    GOSUB PIVOT.RIGHT
  END ELSE
    * Move
    PRINT @(GUARD.COL-1, GUARD.ROW-1):'.':
    GUARD.ROW += D.ROW
    GUARD.COL += D.COL
    PRINT @(GUARD.COL-1, GUARD.ROW-1):CH:
  END
REPEAT
*
POSITIONS=0
FOR R=1 TO DCOUNT(MAP.VISITED,@AM)
  FOR C=1 TO DCOUNT(MAP.VISITED<R>,@VM)
    POSITIONS+=MAP.VISITED<R,C>
  NEXT C
NEXT R
CRT 'Positions visited=':POSITIONS
STOP
*
GET.DIR:
  LOCATE DIR IN DIRS<1> SETTING POS THEN
    D.ROW=DIRS<2,POS>
    D.COL=DIRS<3,POS>
    CH=DIRS<4,POS>
  END
RETURN
*
GET.NEXT.POS:
  NEXT.ROW = GUARD.ROW + D.ROW
  NEXT.COL = GUARD.COL + D.COL
  IF NEXT.ROW > MAX.ROWS OR NEXT.COL > MAX.COLS OR NEXT.ROW = 0 OR NEXT.COL = 0 THEN DONE=1
  CELL=MAP<NEXT.ROW>[NEXT.COL,1]
  IF CELL = '#' THEN BLOCKED=1 ELSE BLOCKED=0
RETURN
*
PIVOT.RIGHT:
  LOCATE DIR IN DIRS<1> SETTING POS THEN
    IF POS=4 THEN POS=1 ELSE POS += 1
    DIR=DIRS<1,POS>
    GOSUB GET.DIR
  END
RETURN
*
