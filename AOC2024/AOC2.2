* https://adventofcode.com/2024/day/2
OSREAD LIST FROM 'AOC2.DAT' ELSE ABORT
CONVERT CHAR(10) TO @AM IN LIST
CONVERT ' ' TO @VM IN LIST
*
TOTAL.SAFE=0
FOR F=1 TO DCOUNT(LIST,@AM)
  IF LIST<F>='' THEN CONTINUE
  TMP.LIST=LIST<F>
  GOSUB CHECK.SAFE
  IF SAFE THEN TOTAL.SAFE+=1 ; CONTINUE
  *
  * Brute force: try removing each element in turn, we just need one to be safe
  *
  FOR B=1 TO DCOUNT(LIST<F>,@VM)
    TMP.LIST=LIST<F>
    DEL TMP.LIST<1,B>
    GOSUB CHECK.SAFE
    IF SAFE THEN TOTAL.SAFE+=1 ; EXIT
  NEXT B
NEXT F
*
CRT 'Total Safe: ':TOTAL.SAFE
STOP
*
CHECK.SAFE:
  SAFE=1
  INCREASING=TMP.LIST<1,1> < TMP.LIST<1,2>
  FOR G=2 TO DCOUNT(TMP.LIST<1>,@VM)
    VAL1=TMP.LIST<1,G-1>
    VAL2=TMP.LIST<1,G>
    DELTA=ABS(VAL2-VAL1)
    IF INCREASING AND VAL2 < VAL1 THEN SAFE=0 ; EXIT
    IF NOT(INCREASING) AND VAL2 > VAL1 THEN SAFE=0 ; EXIT
    IF DELTA < 1 OR DELTA > 3 THEN SAFE=0 ; EXIT
  NEXT G
RETURN
